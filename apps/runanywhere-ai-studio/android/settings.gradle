pluginManagement {
  // Linked manually to used version from our RN fork
  includeBuild(new File(rootDir, "../../../react-native-lab/react-native/packages/gradle-plugin").toString())

  repositories {
    mavenCentral()
    gradlePluginPortal()
    mavenLocal()
    google()
  }
}
plugins { id("com.facebook.react.settings") }

extensions.configure(com.facebook.react.ReactSettingsExtension) { ex ->
  def command = [
    'node',
    '--no-warnings',
    '--eval',
    'require(require.resolve(\'expo-modules-autolinking\', { paths: [require.resolve(\'expo/package.json\')] }))(process.argv.slice(1))',
    'react-native-config',
    '--json',
    '--platform',
    'android'
  ].toList()
  ex.autolinkLibrariesFromCommand(command)
}

include ':app'

apply from: new File(["node", "--print", "require.resolve('expo/package.json')"].execute(null, rootDir).text.trim(), "../scripts/autolinking.gradle")
apply from: new File(rootDir, "versioning_linking.gradle")

// Linked manually to used version from our RN fork
includeBuild(new File(rootDir, "../../../react-native-lab/react-native/packages/gradle-plugin").toString())

include ':expoview'
include ':tools'
include ':packages:react-native:ReactAndroid'
project(':packages:react-native:ReactAndroid').projectDir = new File(rootDir, '../../../react-native-lab/react-native/packages/react-native/ReactAndroid')
dependencyResolutionManagement {
  versionCatalogs {
    defaultLibrariesExtensionName = "expoLibs"
    libs {
      from(files("../../../react-native-lab/react-native/packages/react-native/gradle/libs.versions.toml"))
    }
  }
}
include ':packages:react-native:ReactAndroid:hermes-engine'
project(':packages:react-native:ReactAndroid:hermes-engine').projectDir = new File(rootDir, '../../../react-native-lab/react-native/packages/react-native/ReactAndroid/hermes-engine')
include ':expo-modules-test-core'
project(':expo-modules-test-core').projectDir = new File(rootDir, '../../../packages/expo-modules-test-core/android')

[
    // ADD_NEW_SUPPORTED_ABIS_HERE
].forEach({ abiVariant ->
  include ":expoview-$abiVariant"
  project(":expoview-$abiVariant").projectDir = new File(rootDir, "versioned-abis/expoview-$abiVariant")
})

useExpoModules([
    searchPaths: [
        '../../../packages'
    ],
    exclude : [
        'expo-module-template',
        'expo-module-template-local',
        'react-native-reanimated',
        'expo-dev-menu-interface',
        'expo-dev-menu',
        'expo-dev-launcher',
        'expo-dev-client',
        'expo-maps',
        'expo-network-addons',
        'expo-splash-screen'
    ]
])

// =============================================================================
// RunAnywhere Native Modules (only addition to standard Expo Go)
// =============================================================================
include ':react-native-nitro-modules'
project(':react-native-nitro-modules').projectDir =
    new File(rootDir, '../../../node_modules/react-native-nitro-modules/android')

include ':runanywhere_core'
project(':runanywhere_core').projectDir =
    new File(rootDir, '../../../node_modules/@runanywhere/core/android')

include ':runanywhere_llamacpp'
project(':runanywhere_llamacpp').projectDir =
    new File(rootDir, '../../../node_modules/@runanywhere/llamacpp/android')

include ':runanywhere_onnx'
project(':runanywhere_onnx').projectDir =
    new File(rootDir, '../../../node_modules/@runanywhere/onnx/android')

// =============================================================================
// SDK Peer Dependencies (react-native-fs, react-native-zip-archive)
// These are required by @runanywhere/core for file operations
// =============================================================================
include ':react-native-fs'
project(':react-native-fs').projectDir =
    new File(rootDir, '../../../node_modules/react-native-fs/android')

include ':react-native-zip-archive'
project(':react-native-zip-archive').projectDir =
    new File(rootDir, '../../../node_modules/react-native-zip-archive/android')
